;* Yggdrasil (TM) Core Operating System (x86): Character Library
;* Copyright (C) DeRemee Systems, IXE Electronics LLC
;* Portions copyright IXE Electronics LLC, Republic Robotics, FemtoLaunch, FemtoSat, FemtoTrack, Weland
;* This work is made available under the Creative Commons Attribution-NonCommercial-ShareAlike 4.0 International License.
;* To view a copy of this license, visit http://creativecommons.org/licenses/by-nc-sa/4.0/.

;CONVERTS THE ASCII CHARACTER IN AL TO AN INTEGER
;ON ENTRY:
; AL  = ASCII NUMBER (0 - 9)
;ON RETURN:
; CF  = 0 IF VALUE IS NUMBER
; AL  = VALUE
; CF  = 1 IF VALUE IS NOT NUMBER
; AL  = 0x00
LCHAR2INT:
    ;CHARACTER < '0'?
    CMP   AL, 0x30
    JC    LCHAR2INTA
    ;CHARACTER >= '0'
    ;CONVERT TO INTEGER
    SUB   AL, 0x30
    ;CHARACTER > '9'?
    CMP   AL, 0x0A
    JNC   LCHAR2INTA
    ;CHARACTER =< 9
    RET
  LCHAR2INTA:
    ;CHARACTER NOT A NUMBER
    XOR   AL, AL
    STC
    RET


;CONVERTS THE CHARACTER IN AL TO LOWERCASE
;ON ENTRY:
; AL  = CHARACTER
;ON RETURN:
; CF  = 0 IF VALUE WAS UPPERCASE
;   AL  = CHARACTER
; CF  = 1 IF VALUE WAS NOT UPPERCASE
;   AL  = VALUE ON ENTRY
LCHAR2LOWER:
    ;CHARACTER < 'A'?
    CMP   AL, 0x41
    JC    LCHAR2LOWERA
    ;CHARACTER >= 'A'
    ;CHARACTER > 'Z'?
    CMP   AL, 0x5B
    JNC   LCHAR2LOWERA
    ;CHARACTER <= 'Z'
    ;CONVERT TO LOWERCASE
    ADD   AL, 0x20
    RET
  LCHAR2LOWERA:
    STC
    RET


;CONVERTS THE CHARACTER IN AL TO UPPERCASE
;ON ENTRY:
; AL  = CHARACTER
;ON RETURN:
; CF  = 0 IF VALUE WAS LOWERCASE
;   AL  = CHARACTER
; CF  = 1 IF VALUE WAS NOT LOWERCASE
;   AL  = VALUE ON ENTRY
LCHAR2UPPER:
    ;CHARACTER < 'a'?
    CMP   AL, 0x61
    JC    LCHAR2UPPERA
    ;CHARACTER >= 'a'
    ;CHARACTER > 'a'?
    CMP   AL, 0x7B
    JNC   LCHAR2UPPERA
    ;CHARACTER <= 'a'
    ;CONVERT TO UPPERCASE
    SUB   AL, 0x20
    RET
  LCHAR2UPPERA:
    STC
    RET
    
    
;RETURNS TRUE IF THE CHARACTER IN AL IS AN ARITHMETIC OPERATOR
;ON ENTRY:
; AL  = CHARACTER
;ON RETURN:
; AL  = VALUE ON ENTRY
; CF  = 0 IF CHARACTER NOT ARITHMETIC OPERATOR
; CF  = 1 IF CHARACTER IS ARITHMETIC OPERATOR
LCHARISARITH:
    ;CHARACTER = '!'?
    CMP   AL, 0x21
    JZ    LCHARISARITHA
    ;CHARACTER = '%'?
    CMP   AL, 0x25
    JZ    LCHARISARITHA
    ;CHARACTER = '*'?
    CMP   AL, 0x2A
    JZ    LCHARISARITHA
    ;CHARACTER = '+'?
    CMP   AL, 0x2B
    JZ    LCHARISARITHA
    ;CHARACTER = '-'?
    CMP   AL, 0x2D
    JZ    LCHARISARITHA
    ;CHARACTER = '/'?
    CMP   AL, 0x2F
    JZ    LCHARISARITHA
    ;CHARACTER = '='?
    CMP   AL, 0x3D
    JZ    LCHARISARITHA
    ;CHARACTER = '^'?
    CMP   AL, 0x5E
    JZ    LCHARISARITHA
    ;CHARACTER IS NOT ARITHMETIC OPERATOR
    CLC
    RET
  LCHARISARITHA:
    ;CHARACTER IS ARITHMETIC OPERATOR
    STC
    RET
    
    
;RETURNS TRUE IF THE CHARACTER IN AL IS A CLOSING BRACKET
;ON ENTRY:
; AL  = CHARACTER
;ON RETURN:
; AL  = VALUE ON ENTRY
; CF  = 0 IF CHARACTER NOT CLOSING BRACKET
; CF  = 1 IF CHARACTER IS CLOSING BRACKET
LCHARISBRACC:
    CHARACTER = ')'?
    CMP   AL, 0x29
    JZ    LCHARISBRACCA
    CHARACTER = ']'?
    CMP   AL, 0x5D
    JZ    LCHARISBRACCA
    CHARACTER = '}'?
    CMP   AL, 0x7D
    JZ    LCHARISBRACCA
    ;CHARACTER IS NOT CLOSING BRACKET
    CLC
    RET
  LCHARISBRACCA:
    ;CHARACTER IS CLOSING BRACKET
    STC
    RET
    

;RETURNS TRUE IF THE CHARACTER IN AL IS AN OPENING BRACKET
;ON ENTRY:
; AL  = CHARACTER
;ON RETURN:
; AL  = VALUE ON ENTRY
; CF  = 0 IF CHARACTER NOT OPENING BRACKET
; CF  = 1 IF CHARACTER IS OPENING BRACKET
LCHARISBRACO:
    CHARACTER = '('?
    CMP   AL, 0x28
    JZ    LCHARISBRACOA
    CHARACTER = '['?
    CMP   AL, 0x5B
    JZ    LCHARISBRACOA
    CHARACTER = '{'?
    CMP   AL, 0x7B
    JZ    LCHARISBRACOA
    ;CHARACTER IS NOT OPENING BRACKET
    CLC
    RET
  LCHARISBRACOA:
    ;CHARACTER IS OPENING BRACKET
    STC
    RET
    
    
;RETURNS TRUE IS THE CHARACTER IN AL IS A HEXADECIMAL DIGIT
;ON ENTRY:
; AL  = CHARACTER
;ON RETURN:
; AL  = VALUE ON ENTRY
; CF  = 0 IF CHARACTER NOT HEX DIGIT
; CF  = 1 IF CHARACTER IS HEX DIGIT
LCHARISHEX:
    ;CHARACTER < '0'?
    CMP   AL, 0x30
    JC    LCHARISHEXA
    ;CHARACTER >= '0'
    ;CHARACTER <= '9'?
    CMP   AL, 0x3A
    JC   LCHARISHEXB
    ;CHARACTER < 'A'?
    CMP   AL, 0x41
    JC    LCHARISHEXA
    ;CHARACTER >= 'A'
    ;CHARACTER <= 'F'?
    CMP   AL, 0x47
    JC   LCHARISHEXB
    ;CHARACTER < 'a'?
    CMP   AL, 0x61
    JC    LCHARISHEXA
    ;CHARACTER >= 'a'
    ;CHARACTER <= 'f'?
    CMP   AL, 0x67
    JC   LCHARISHEXB
  LCHARISHEXA:
    CLC
  LCHARISHEXB:
    RET
    
    
;RETURNS TRUE IF THE CHARACTER IN AL IS A LETTER
;ON ENTRY:
; AL  = CHARACTER
;ON RETURN:
; AL  = VALUE ON ENTRY
; CF  = 0 IF CHARACTER NOT LETTER
; CF  = 1 IF CHARACTER IS LETTER
LCHARISLETTER:
    ;CHARACTER < 'A'?
    CMP   AL, 0x41
    JC    LCHARISLETTERA
    ;CHARACTER >= 'A'
    ;CHARACTER <= 'Z'?
    CMP   AL, 0x5B
    JC   LCHARISLETTERB
    ;CHARACTER > 'Z'
    ;CHARACTER < 'a'?
    CMP   AL, 0x61
    JC    LCHARISLETTERA
    ;CHARACTER >= 'a'
    ;CHARACTER <= 'z'?
    CMP   AL, 0x7B
    JC   LCHARISLETTERB
  LCHARISLETTERA:
    CLC
  LCHARISLETTERB:
    RET
    
    
;RETURNS TRUE IF THE CHARACTER IN AL IS A LOGIC OPERATOR
;ON ENTRY:
; AL  = CHARACTER
;ON RETURN:
; AL  = VALUE ON ENTRY
; CF  = 0 IF CHARACTER NOT LOGIC OPERATOR
; CF  = 1 IF CHARACTER IS LOGIC OPERATOR
LCHARISLOGIC:
    ;CHARACTER = '!'?
    CMP   AL, 0x21
    JZ    LCHARISLOGICA
    ;CHARACTER = '&'?
    CMP   AL, 0x25
    JZ    LCHARISLOGICA
    ;CHARACTER = '^'?
    CMP   AL, 0x5E
    JZ    LCHARISLOGICA
    ;CHARACTER = '|'?
    CMP   AL, 0x7C
    JZ    LCHARISLOGICA
    ;CHARACTER IS NOT LOGIC OPERATOR
    CLC
    RET
  LCHARISLOGICA:
    ;CHARACTER IS LOGIC OPERATOR
    STC
    RET
    
    
;RETURNS TRUE IF THE CHARACTER IN AL IS A NUMBER
;ON ENTRY:
; AL  = CHARACTER
;ON RETURN:
; AL  = VALUE ON ENTRY
; CF  = 0 IF CHARACTER NOT NUMBER
; CF  = 1 IF CHARACTER IS NUMBER
LCHARISNUM:
    ;CHARACTER < '0'?
    CMP   AL, 0x30
    JC    LCHARISNUMA
    ;CHARACTER >= '0'
    ;CHARACTER > '9'?
    CMP   AL, 0x3A
    JNC   LCHARISNUMA
    ;CHARACTER <= '9'
    RET
  LCHARISNUMA:
    CLC
    RET
    

;RETURNS TRUE IF THE CHARACTER IN AL IS PUNCTUATION
;ON ENTRY:
; AL  = CHARACTER
;ON RETURN:
; AL  = VALUE ON ENTRY
; CF  = 0 IF CHARACTER NOT PUNCTUATION
; CF  = 1 IF CHARACTER IS PUNCTUATION
LCHARISPUNCT:
    ;CHARACTER = '!'?
    CMP   AL, 0x21
    JZ    LCHARISPUNCTA
    ;CHARACTER = '.'?
    CMP   AL, 0x2E
    JZ    LCHARISPUNCTA
    ;CHARACTER = '?'?
    CMP   AL, 0x3F
    JZ    LCHARISPUNCTA
    ;CHARACTER IS NOT PUNCTUATION
    CLC
    RET
  LCHARISPUNCTA:
    ;CHARACTER IS PUNCTUATION
    STC
    RET
    

;RETURNS TRUE IF THE CHARACTER IN AL IS A RELATIONAL OPERATOR
;ON ENTRY:
; AL  = CHARACTER
;ON RETURN:
; AL  = VALUE ON ENTRY
; CF  = 0 IF CHARACTER NOT RELATIONAL OPERATOR
; CF  = 1 IF CHARACTER IS RELATIONAL OPERATOR
LCHARISREL:
    ;CHARACTER < '<'?
    CMP   AL, 0x3C
    JC    LCHARISRELA
    ;CHARACTER >= '<'
    ;CHARACTER > '>'?
    CMP   AL, 0x3F
    JNC   LCHARISRELA
    ;CHARACTER <= '>'
    RET
  LCHARISRELA:
    CLC
    RET
